#!./gawkldr -f
#
# VitaminA logging plugin
#
# Copyright PÅ™emysl Janouch 2010. All rights reserved.
# See the file LICENSE for licensing information.
#
#

@include utils.awk

BEGIN \
{
	RS = "\r\n"
	ORS = RS

	# Hook all messages
	print "all"
	fflush("")
}

!initialized \
{
	# Get a directory to store log files to
	logsdir = getconfig("datadir") "/logs"
	if (system("test -d '" logsdir "'"))
		system("mkdir -p '" logsdir "'")
	initialized = 1
}

function logline (line, channel)
{
	time = strftime("%D %T | ")
	botprint(time channel " | " line)
	print(time line) >> (logsdir "/" channel strftime(" %Y.%m.%d.log"))
	fflush("")
}

{
	parse($0)
	match(mwho, /^[^@!]+/)
	nick = substr(mwho, RSTART, RLENGTH)
}

mcmd == "PRIVMSG" \
{
	logline(" - " nick ": " mparam[1], mparam[0])
}

mcmd == "KICK" \
{
	logline(mparam[1] " was kicked from " mparam[0] " by " nick, mparam[0])
}

mcmd == "NOTICE" \
{
	logline("Notice from " nick ": " mparam[1], mparam[0])
}

mcmd == "JOIN" \
{
	logline(nick " has joined " mparam[0], mparam[0])
}

mcmd == "PART" \
{
	logline(nick " has left " mparam[0], mparam[0])
}

mcmd == "MODE" \
{
	s = 0
	t = ""
	for (i in mparam)
		s++
	for (i = 1; i < s; i++)
		t = t " " mparam[i]
	logline("Mode" t " by " nick, mparam[0])
}

{
	print ""
	fflush("")
}

